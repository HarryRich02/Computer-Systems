#===INPUT===
inp IN      #Take user input

#===MAIN-LOOP===
nex STO n   #Store value in n and output
    OUT
    SUB 1   #Check if n is 1, if so halt without output
    BRZ 0
    LDA 0   #Set tem to 0
    STO tem

#===DIVIDE-BY-2===
div LDA tem #Increment tem by 1 (calculating n/2)
    ADD 1
    STO tem
    LDA n   #Decrement n by 2 (counting down to end loop)
    SUB 2
    STO n
    BRZ loo #If n is 0, the number is even and the loop has ended, so repeat function
    BRP div #Repeat until fully divided

#===IF-ODD===
odd LDA tem #Check if value will overflow when multiplied by 3, and if so halt outputting 0
    SUB 334
    BRP ove
    
    LDA tem #Multiply tem by 3 and subtract 1
    ADD tem
    SUB 1

loo ADD tem #Load tem and return to start of loop
    BR nex

#===HALT===
ove LDA 0 #Output 0
    OUT
0   DAT 0 #Halt program

#===VARIABLES===
n   DAT 0
tem DAT 0
1   DAT 1
2   DAT 2
334 DAT 334